#!/usr/bin/zsh
#
# .zshrc - Zsh file loaded on interactive shell sessions.
#

# Instant Prompt
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Load zprof first if we need to profile.
[[ ${ZPROFRC:-0} -eq 0 ]] || zmodload zsh/zprof
alias zprofrc="ZPROFRC=1 zsh"

# Lazy and autoload functions
for funcdir in $ZDOTDIR/functions $ZDOTDIR/functions/*(N/); do
  fpath=($funcdir $fpath)
  autoload -Uz $fpath[1]/*(.:t)
done
unset funcdir

# Load additional zshrc files
for _zrc in $ZDOTDIR/zshrc.d/*.zsh; source $_zrc; unset _zrc

# Source zstyle configurations for plugins
[[ -e $ZDOTDIR/.zstyles ]] && source $ZDOTDIR/.zstyles

# Clone antidote if necessary
[[ -d $ZDOTDIR/antidote ]] ||
  git clone https://github.com/mattmc3/antidote $ZDOTDIR/antidote

# Load antidote
source $ZDOTDIR/antidote/antidote.zsh
antidote load

# Load aliases
[[ -e $ZDOTDIR/.zsh_aliases ]] && source $ZDOTDIR/.zsh_aliases
